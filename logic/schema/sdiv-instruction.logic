// http://llvm.org/docs/LangRef.html#sdiv-instruction
// keywords: arithmetic; binary; instruction

sdiv_instruction(Insn) -> instruction(Insn).

sdiv_instruction:first_operand[Insn] = Left ->
   sdiv_instruction(Insn), operand(Left).

sdiv_instruction:second_operand[Insn] = Right ->
   sdiv_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘sdiv‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

sdiv_instruction(Insn) -> sdiv_instruction:first_operand[Insn] = _.
sdiv_instruction(Insn) -> sdiv_instruction:second_operand[Insn] = _.


sdiv_instruction(Insn), instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).


instruction:type[Insn] = Type
 , sdiv_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.


instruction:type[Insn] = Type
 , sdiv_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
