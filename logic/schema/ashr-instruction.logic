// http://llvm.org/docs/LangRef.html#ashr-instruction
// keywords: bitwise; binary; instruction

ashr_instruction(Insn) -> instruction(Insn).

ashr_instruction:first_operand[Insn] = Left -> 
   ashr_instruction(Insn), operand(Left).

ashr_instruction:second_operand[Insn] = Right -> 
   ashr_instruction(Insn), operand(Right).

// Constraints:
// The two arguments to the ‘ashr‘ instruction must be:
//    (i)  integer or 
//    (ii) vector of integer values. 
// Both arguments must have identical types.

ashr_instruction(Insn) -> ashr_instruction:first_operand[Insn] = _.
ashr_instruction(Insn) -> ashr_instruction:second_operand[Insn] = _.

ashr_instruction(Insn),
   instruction:type[Insn] = Type
 -> 
   integer_type(Type); vector_type:integer(Type).

instruction:type[Insn] = Type,
   ashr_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

instruction:type[Insn] = Type,
   ashr_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
