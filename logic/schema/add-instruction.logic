//http://llvm.org/docs/LangRef.html#add-instructions
// keywords: arithmetic; binary; instruction

add_instruction(Insn) -> instruction(Insn).

add_instruction:first_operand[Insn] = Left -> 
   add_instruction(Insn), operand(Left).

add_instruction:second_operand[Insn] = Right -> 
   add_instruction(Insn), operand(Right).

// Constraints:
// The two arguments to the ‘add‘ instruction must be:
//    (i)  integer or 
//    (ii) vector of integer values. 
// Both arguments must have identical types.

add_instruction(Insn) -> add_instruction:first_operand[Insn] = _.
add_instruction(Insn) -> add_instruction:second_operand[Insn] = _.

add_instruction(Insn),
   instruction:type[Insn] = Type
 -> 
   integer_type(Type); vector_type:integer(Type).

instruction:type[Insn] = Type,
   add_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

instruction:type[Insn] = Type,
   add_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
