// http://llvm.org/docs/LangRef.html#shufflevector-instruction
// keywords: vector; instruction

shufflevector_instruction(Insn) -> instruction(Insn).

shufflevector_instruction:first_vector[Insn] = First ->
   shufflevector_instruction(Insn), operand(First).

shufflevector_instruction:second_vector[Insn] = Second ->
   shufflevector_instruction(Insn), operand(Second).

shufflevector_instruction:mask[Insn] = Mask ->
   shufflevector_instruction(Insn), immediate(Mask).

// The type of the two input vectors.
shufflevector_instruction:input_type[Insn] = Type <-
   shufflevector_instruction:first_vector[Insn] = Vector,
   operand:type[Vector] = Type.

// The type of the elements of the two input vectors.
shufflevector_instruction:input_comp[Insn] = Comp <-
   shufflevector_instruction:input_type[Insn] = Type,
   vector_type:component[Type] = Comp.

// The type of the shuffle mask.
shufflevector_instruction:mask_type[Insn] = Type <-
   shufflevector_instruction:mask[Insn] = Mask,
   operand:type[Mask] = Type.

// The type of the elements of the shuffle mask.
shufflevector_instruction:mask_comp[Insn] = Comp <-
   shufflevector_instruction:mask_type[Insn] = Type,
   vector_type:component[Type] = Comp.


// Constraints:
//
// The 'shufflevector' instruction takes three arguments:
//  (i)  two vectors of the same type and
//  (ii) a shuffle mask argument.
// The shuffle mask is required to be:
//  (i) a constant vector
//  (ii) with i32-integer component type.
//
// The result is a vector
//  (i)  whose length is the same as the shuffle mask and
//  (ii) whose element type is the same as the element type of the first two operands.

shufflevector_instruction(Insn) -> shufflevector_instruction:first_vector[Insn]  = _.
shufflevector_instruction(Insn) -> shufflevector_instruction:second_vector[Insn] = _.
shufflevector_instruction(Insn) -> shufflevector_instruction:mask[Insn] = _.

shufflevector_instruction:input_type[_] = Type 
 ->
   vector_type(Type).


shufflevector_instruction(Insn),
   shufflevector_instruction:input_type[Insn] = Type1,  
   shufflevector_instruction:second_vector[Insn] = Vector,
   operand:type[Vector] = Type2
 ->
   Type1 = Type2.


shufflevector_instruction:mask_type[_] = Type
 ->
   vector_type(Type).


shufflevector_instruction:mask_comp[_] = Type
 ->
    Type = int32_type[].


shufflevector:instruction(Insn),
   instruction:type[Insn] = Type
 ->
   vector_type(Type).


instruction:type[Insn] = Type1,
   shufflevector_instruction:mask_type[Insn] = Type2
 ->
   vector:compatible(Type1, Type2).
   

instruction:type[Insn] = Type,
   vector_type:component[Type] = Comp1,
   shufflevector_instruction:input_comp[Insn] = Comp2
 ->
   Comp1 = Comp2.
