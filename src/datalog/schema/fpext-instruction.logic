// http://llvm.org/docs/LangRef.html#fpext-to-instruction
// keywords: conversion; unary; instruction

fpext_instruction(Insn) -> instruction(Insn).

fpext_instruction:from[Insn] = Value -> 
   fpext_instruction(Insn), operand(Value).

fpext_instruction:to_type[Insn] = Type -> 
   fpext_instruction(Insn), type(Type).

fpext_instruction:from_type[Insn] = Type <-
   fpext_instruction:from[Insn] = Value,
   operand:type[Value] = Type.

// Constraints:
//
// The ‘fpext‘ instruction takes:
//  (i)  a floating point value to cast,
//  (ii) and a floating point type to cast it to.
//
// The source type must be smaller than the destination type.

fpext_instruction(Insn) -> fpext_instruction:from[Insn] = _.
fpext_instruction(Insn) -> fpext_instruction:to_type[Insn] = _.

fpext_instruction:to_type[Insn] = Type
 ->
   instruction:type[Insn] = Type.


fpext_instruction:from_type[_] = Type
 -> 
   fp_type(Type) ; 0 = 1. // TODO: check if dummy or above can be avoided in future versions


fpext_instruction:to_type[_] = Type
 ->
   fp_type(Type).
